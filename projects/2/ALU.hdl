/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //----------------------------------------------------------------------------------------    
    // if (zx == 1) sets x = 0 (16-bit constant)                                             
    // if (nx == 1) sets x = !x (bitwise not)                                                
    //
    // Разобьем на группы zx,nx, чтобы было удобно работать с Mux
    //
    // Если zx = 1, продвигаем b которое сделали нулями, если нет,продвигаем исходное число
    Mux16(a=x, b=false, sel=zx, out=outXOrZeroX); 
    // Делаем инвентор для нашего исходного числа, для nx
    Not16(in=outXOrZeroX, out=NotXOrZeroX); 
    // Если nx = 1, продвигаем в out наш инвентор, если оно 0, продвигаем число предыдущего
    // этапа в след. этап
    Mux16(a=outXOrZeroX, b=NotXOrZeroX, sel=nx, out=outX); 
    //----------------------------------------------------------------------------------------
    //
    // if (zy == 1) sets y = 0 (16-bit constant)
    // if (ny == 1) sets y = !y (bitwise not)
    //
    // Разобьем на группы zy,ny (логика такая же)
    //
    // Если zx = 1, продвигаем b которое сделали нулями, если нет,продвигаем исходное число
    Mux16(a=y, b=false, sel=zy, out=outYOrZeroY); 
    // Делаем инвентор для нашего исходного числа, для ny
    Not16(in=outYOrZeroY, out=NotYOrZeroY); 
    // Если ny = 1, продвигаем в out наш инвентор, если оно 0, продвигаем число предыдущего
    // этапа в след. этап
    Mux16(a=outYOrZeroY, b=NotYOrZeroY, sel=ny, out=outY); 
    //----------------------------------------------------------------------------------------
    //    
    // if (f == 1)  sets out = x + y (integer 2's complement addition)
    // if (f == 0)  sets out = x & y (bitwise and)
    // if (no == 1) sets out = !out (bitwise not)
    //
    // Разобьем на группы f,no
    // 
    // Сначала выполним сложение операнов передущих этапов outX и outY, чтобы их сумму использовать 
    // по необходимости
    Add16(a=outX, b=outY, out=xSUMy);
    // Так же сделаем битовое И чтобы использрвать это по необходимости
    And16(a=outX, b=outY, out=xANDy); 
    // Толкаем дальше нужный результат в зависимоти от состояния f
    Mux16(a=xANDy, b=xSUMy, sel=f, out=outF);
    Not16(in=outF, out=NotoutF);
    // // Толкаем дальше нужный результат в зависимоти от состояния no
    Mux16(a=outF, b=NotoutF, sel=no, out=outNo);    
    Mux16(a=outF, b=NotoutF, sel=no, out=out, out[0..7]=out0to7, out[8..15]=out8to15, out[15]=ng); 
    //----------------------------------------------------------------------------------------
    //
    //
    // Установка флагов zr и ng
    // zr == if (out == 0) equals 1, else 0
    Or8Way(in=out0to7, out=outLow);
    Or8Way(in=out8to15, out=outHigh);
    Or(a=outLow, b=outHigh, out=outResultOr);
    Not(in=outResultOr, out=zr);
    //-----------------------------------------------------------------------------------------------------------------------------------
}
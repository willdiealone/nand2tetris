/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    // reset > load > inc > hold приоритет операций
    // Закидываем значение в регистр
    Register(in=next, load=true, out=value);
    // Пробрасываем наружу value
    Or16(a=value, b=false, out=out);
    // Увеличивем счетчик
    Inc16(in=value, out=valuePlus1);
    // Если inc1, то делаем инкремент нашего числа
    Mux16(a=value, b=valuePlus1, sel=inc, out=valueOrvaluePlus1);
    // Если load1, то делаем запись нашего числа in
    Mux16(a=valueOrvaluePlus1, b=in, sel=load, out=loadInOrvalueOrvaluePlus1);
    // Проверяем сброс, если reset 1, то обнуляем наше текущее значение 
    Mux16(a=loadInOrvalueOrvaluePlus1, b=false, sel=reset, out=next);
    
}

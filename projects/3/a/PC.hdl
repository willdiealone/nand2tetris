/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    // reset > load > inc > hold
    // Закидываем значение в регистр
    Register(in=next, load=true, out=value);
    // Пробрасываем наружу value
    Or16(a=value, b=false, out=out);
    // Увеличивем счетчик
    Inc16(in=value, out=valuePlus1);
    // inc? value+1 : value
    Mux16(a=value, b=valuePlus1, sel=inc, out=incOrHold);
    // load? in : (inc/hold)
    Mux16(a=incOrHold, b=in, sel=load, out=loadOrInc);
    // reset? 0 : ...
    Mux16(a=loadOrInc, b=false, sel=reset, out=next);
}

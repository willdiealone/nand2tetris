/**
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // Раздаём сигнал load только одному регистру по адресу
    DMux8Way(in=load, sel=address,a=ld0, b=ld1, c=ld2, d=ld3, e=ld4, f=ld5, g=ld6, h=ld7);
    // 8 регистров (in идёт всем, но load только выбранному)
    Register(in=in, load=ld0, out=o0);
    Register(in=in, load=ld1, out=o1);
    Register(in=in, load=ld2, out=o2);
    Register(in=in, load=ld3, out=o3);
    Register(in=in, load=ld4, out=o4);
    Register(in=in, load=ld5, out=o5);
    Register(in=in, load=ld6, out=o6);
    Register(in=in, load=ld7, out=o7);
    // Чтение: выбираем выход нужного регистра по адресу
    Mux8Way16(a=o0, b=o1, c=o2, d=o3, e=o4, f=o5, g=o6, h=o7,sel=address, out=out);
    
}
